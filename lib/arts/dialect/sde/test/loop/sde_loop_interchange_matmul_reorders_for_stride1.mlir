// RUN: %carts-compile %s --O3 --arts-config %arts_config --pipeline openmp-to-arts --mlir-print-ir-after-all 2>&1 | %FileCheck %s --check-prefix=BEFORE
// RUN: %carts-compile %s --O3 --arts-config %arts_config --pipeline openmp-to-arts --mlir-print-ir-after-all 2>&1 | %FileCheck %s --check-prefix=AFTER

// Verify that LoopInterchange swaps j-k loops to k-j order for stride-1
// B[k,j] access in matmul patterns. Before interchange (after RaiseToTensor),
// the inner loop order is j/k. After interchange, the order should be k/j.

// BEFORE-LABEL: // -----// IR Dump After RaiseToLinalg (raise-to-linalg) //----- //
// BEFORE: arts_sde.su_iterate
// BEFORE-SAME: classification(<matmul>)
// BEFORE: scf.for %[[J:.+]] = %c0 to %c32 step %c1 {
// BEFORE:   scf.for %[[K:.+]] = %c0 to %c32 step %c1 {
// BEFORE:     memref.load %arg0[%{{.*}}, %[[K]]]
// BEFORE:     memref.load %arg1[%[[K]], %[[J]]]

// AFTER-LABEL: // -----// IR Dump After LoopInterchange (loop-interchange) //----- //
// AFTER: arts_sde.su_iterate
// AFTER-SAME: classification(<matmul>)
// AFTER: scf.for %[[K:.+]] = %c0 to %c32 step %c1 {
// AFTER:   scf.for %[[J:.+]] = %c0 to %c32 step %c1 {
// AFTER:     memref.load %arg0[%{{.*}}, %[[K]]]
// AFTER:     memref.load %arg1[%[[K]], %[[J]]]

module attributes {dlti.dl_spec = #dlti.dl_spec<#dlti.dl_entry<f32, dense<32> : vector<2xi64>>, #dlti.dl_entry<i64, dense<64> : vector<2xi64>>, #dlti.dl_entry<i32, dense<32> : vector<2xi64>>, #dlti.dl_entry<!llvm.ptr, dense<64> : vector<4xi64>>, #dlti.dl_entry<"dlti.endianness", "little">, #dlti.dl_entry<"dlti.stack_alignment", 128 : i64>>, llvm.data_layout = "e-m:e-i8:8:32-i16:16:32-i64:64-i128:128-n32:64-S128", llvm.target_triple = "aarch64-unknown-linux-gnu"} {
  func.func @main(%A: memref<32x32xf32>, %B: memref<32x32xf32>, %C: memref<32x32xf32>) {
    %c0 = arith.constant 0 : index
    %c32 = arith.constant 32 : index
    %c1 = arith.constant 1 : index
    omp.parallel {
      omp.wsloop {
        omp.loop_nest (%i) : index = (%c0) to (%c32) step (%c1) {
          scf.for %j = %c0 to %c32 step %c1 {
            scf.for %k = %c0 to %c32 step %c1 {
              %a = memref.load %A[%i, %k] : memref<32x32xf32>
              %b = memref.load %B[%k, %j] : memref<32x32xf32>
              %c = memref.load %C[%i, %j] : memref<32x32xf32>
              %prod = arith.mulf %a, %b : f32
              %sum = arith.addf %c, %prod : f32
              memref.store %sum, %C[%i, %j] : memref<32x32xf32>
            }
          }
          omp.yield
        }
      }
      omp.terminator
    }
    return
  }
}
