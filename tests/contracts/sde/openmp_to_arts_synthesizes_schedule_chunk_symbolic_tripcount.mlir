// RUN: %carts-compile %s --O3 --arts-config %S/../inputs/arts_64t.cfg --pipeline openmp-to-arts --mlir-print-ir-after-all 2>&1 | %FileCheck %s

// Verify that SdeChunkOptimization synthesizes a symbolic chunk size for a
// dynamic loop with no source chunk, keeping the decision at the SDE layer.

// CHECK-LABEL: // -----// IR Dump After SdeChunkOptimization (sde-chunk-optimization) //----- //
// CHECK: func.func @main(%[[N:.*]]: index, %[[A:.*]]: memref<?xf64>, %[[B:.*]]: memref<?xf64>)
// CHECK: %[[SPAN:.*]] = arith.subi %[[N]], %{{.*}} : index
// CHECK: %[[NONNEG:.*]] = arith.maxui %[[SPAN]], %{{.*}} : index
// CHECK: %[[TRIP:.*]] = arith.ceildivui %[[NONNEG]], %{{.*}} : index
// CHECK: %[[CLAMPED_TRIP:.*]] = arith.maxui %[[TRIP]], %{{.*}} : index
// CHECK: %[[BALANCED:.*]] = arith.ceildivui %[[CLAMPED_TRIP]], %{{.*}} : index
// CHECK: %[[PREFERRED:.*]] = arith.maxui %[[BALANCED]], %{{.*}} : index
// CHECK: %[[CHUNK:.*]] = arith.minui %[[PREFERRED]], %[[CLAMPED_TRIP]] : index
// CHECK: arts_sde.su_iterate(%{{.*}}) to(%[[N]]) step(%{{.*}}) schedule(<dynamic>, %[[CHUNK]])

module attributes {dlti.dl_spec = #dlti.dl_spec<#dlti.dl_entry<f64, dense<64> : vector<2xi64>>, #dlti.dl_entry<i64, dense<64> : vector<2xi64>>, #dlti.dl_entry<i32, dense<32> : vector<2xi64>>, #dlti.dl_entry<!llvm.ptr, dense<64> : vector<4xi64>>, #dlti.dl_entry<"dlti.endianness", "little">, #dlti.dl_entry<"dlti.stack_alignment", 128 : i64>>, llvm.data_layout = "e-m:e-i8:8:32-i16:16:32-i64:64-i128:128-n32:64-S128", llvm.target_triple = "aarch64-unknown-linux-gnu"} {
  func.func @main(%N: index, %A: memref<?xf64>, %B: memref<?xf64>) {
    %c0 = arith.constant 0 : index
    %c1 = arith.constant 1 : index
    %cst = arith.constant 2.0 : f64
    omp.parallel {
      omp.wsloop schedule(dynamic, nonmonotonic) {
        omp.loop_nest (%i) : index = (%c0) to (%N) step (%c1) {
          %v = memref.load %A[%i] : memref<?xf64>
          %r = arith.mulf %v, %cst : f64
          memref.store %r, %B[%i] : memref<?xf64>
          omp.yield
        }
      }
      omp.terminator
    }
    return
  }
}
